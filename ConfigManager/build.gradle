apply plugin: 'java'

version = '1.0'

dependencies {
    compile project(':core')
    compile 'org.apache.activemq:activemq-all:5.11.1'
    compile 'org.apache.qpid:qpid-jms-client:0.2.0-SNAPSHOT'
    runtime 'log4j:log4j:1.2.17'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

jar {
    dependsOn configurations.runtime

    manifest {
           attributes 'Implementation-Title': 'Gradle Jar File Example',
                   	'Implementation-Version': version,
                   'Main-Class': 'org.jboss.summit2015.config.DownloadScannerConfig'
   	}

    from {
        (configurations.runtime).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    archiveName = "${project.archivesBaseName}-service.jar"
}

// Tasks to install the scanner-configd and scannerd services
task installSystemdConf(dependsOn: [processResources], type: Copy) {
    from "$buildDir/resources/main/systemd"
    into '/usr/lib/systemd/system'
}
installSystemdConf << {
    println("Installed resources/main/systemd to /usr/lib/systemd/system")
}
task installDaemon(dependsOn: [installSystemdConf, assemble], type: Copy) {
    from "$buildDir/resources/main/bin"
    from "$buildDir/libs"
    into '/usr/local/bin'
}
installDaemon << {
    println("Installed resources/main/bin,libs/*.jar to /usr/local/bin")
}